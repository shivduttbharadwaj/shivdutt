<?xml version="1.0" encoding="UTF-8"?>
<BES xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="BES.xsd">
	<Fixlet>
		<Title>Prepare Endpoint Tools and JRE (WIN 64-bit)</Title>
		<Description><![CDATA[<P>Prepare the endpoint for GCM health checks and remediation and create __UIDEXT folder if not exists.</P>
<P>Endpoints must have at least 504 MB available on the file system where the BES client is located for this fixlet to become applicable.</P>
<P><SMALL><B>Version:</B>&nbsp;4.0.0<BR><B>Build ID:</B>&nbsp;20190307-2125</SMALL></P>
<P>----------------------------------------------------------------------------------<BR>Licensed Materials - Property of IBM<BR>(c) Copyright IBM Corp. 2019. All Rights Reserved.</P>]]></Description>
		<Relevance>windows of operating system</Relevance>
		<Relevance>x64 of operating system</Relevance>
		<Relevance>not exists folder "__GTS/jre" of storage folder of client</Relevance>
		<Relevance><![CDATA[(version of client >= "9.2")]]></Relevance>
		<Relevance>disjunction of ((name of operating system as lowercase contains it) of ( "win2008"; "win2012"; "win2016";"win7" ))</Relevance>
		<Relevance><![CDATA[
		(if name of operating system as lowercase contains "win" then
(
	(free space of drive of storage folder of client)
)
else 
(
	if name of operating system as lowercase contains "sunos" then
	(
		 ((free space of filesystem of storage folder of client) - (total space of filesystem of storage folder of client / 100))
	)
	else	
	(
		(free space of filesystem of storage folder of client)
	)
)
) > 528928641
		]]></Relevance>
		<Category>GCM</Category>
		<Source>GCM</Source>
		<SourceID>4.0.0</SourceID>
		<SourceReleaseDate>2019-03-07</SourceReleaseDate>
		<SourceSeverity></SourceSeverity>
		<CVENames></CVENames>
		<SANSID></SANSID>
		<MIMEField>
			<Name>x-fixlet-ibm-copyright</Name>
			<Value>Licensed Materials - Property of IBM; (c) Copyright IBM Corp. 2019. All Rights Reserved.</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-gcm-author</Name>
			<Value>GTS GCM Team</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-gcm-origin</Name>
			<Value>GTS GCM Team</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-requested-id</Name>
			<Value>11</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-modification-time</Name>
			<Value>Wed, 27 Mar 2019 10:23:41 +0000</Value>
		</MIMEField>
		<Domain>BESC</Domain>
		<DefaultAction ID="Action1">
			<Description>
				<PreLink>Click </PreLink>
				<Link>here</Link>
				<PostLink> to deploy this action.</PostLink>
			</Description>
			<ActionScript MIMEType="application/x-Fixlet-Windows-Shell"><![CDATA[
			
			// Licensed Materials - Property of IBM; (c) Copyright IBM Corporation 2019. All Rights Reserved.
			
begin prefetch block

if {exists setting "_GCM_Download_BasePath" whose (value of it != "") of client}
	parameter "basepath" = "{value of setting "_GCM_Download_BasePath" of client}"
else
	parameter "basepath" = "http://localhost:52311/iemhc/20190307-2125"
endif

// download HCLauncher
add prefetch item name=HCLauncher.jar sha1=b8c64af8249e34fe07c4f2ea700916f12f6b6f8c sha256=9c7e489131cf1485d389936a0119ec8b19f7a84c3db151bc1d2401ce5d30ab08 size=8886900 url={parameter "basepath"}/400/20190307-2125/HCLauncher/HCLauncher.jar 

// download JRE 

add prefetch item name=ibm-java-jre-80-win-x86_64.zip sha1=1f99a4cfbfe957a5919d76f3056897a5c188a63c sha256=6ded876650d7edef6a7f41867015338cd062a5ca34eb37f3ed7d35c1457a2fea size=173347247 url={parameter "basepath"}/jre/80530/ibm-java-jre-80-win-x86_64.zip

add prefetch item name=unzip.exe sha1=84debf12767785cd9b43811022407de7413beb6f sha256=2122557d350fd1c59fb0ef32125330bde673e9331eb9371b454c2ad2d82091ac size=204800 url={parameter "basepath"}/400/20190307-2125/tools/unzip.exe
end prefetch block

parameter "build_id" = "20190307-2125"
parameter "version" = "4.0.0"

if {name of operating system as lowercase contains "sunos" OR name of operating system as lowercase contains "hp-ux"}
	parameter "dump" = ""
else
	parameter "dump" = "-Xdump:none"
endif

// re-create base folder if not exists
folder create "{storage folder of client}/__GTS/__IEMHC"			
// clean and add the HCLauncher.jar
folder delete "{storage folder of client}/__GTS/__IEMHC/HCLauncher"
folder delete "{storage folder of client}/__GTS/HCLauncher_400"
folder create "{storage folder of client}/__GTS/HCLauncher_400"
folder create "{storage folder of client}/__GTS/__UIDEXT"

copy "{client folder of current site}/__Download/HCLauncher.jar" "{storage folder of client}/__GTS/HCLauncher_400/HCLauncher.jar"
			
// clean the JRE location
delete "{storage folder of client}/__GTS/__IEMHC/license_en.txt"
delete "{storage folder of client}/__GTS/__IEMHC/notices.txt"
delete "{storage folder of client}/__GTS/__IEMHC/readme.txt"
delete "{storage folder of client}/__GTS/__IEMHC/version.txt"
delete "{storage folder of client}/__GTS/__IEMHC/copyright"
folder delete "{storage folder of client}/__GTS/__IEMHC/docs"
folder delete "{storage folder of client}/__GTS/__IEMHC/properties"
folder delete "{storage folder of client}/__GTS/jre"
folder delete "{storage folder of client}/__GTS/__IEMHC/jre"

// JRE extract and install

// clean the temporary storage
folder delete "{storage folder of client}/__GTS/Temp"
folder create "{storage folder of client}/__GTS/Temp"

wait "{client folder of current site}\__Download\unzip.exe" -o "{client folder of current site}\__Download\ibm-java-jre-80-win-x86_64.zip" -d "{storage folder of client}\__GTS"    
appendfile 6ded876650d7edef6a7f41867015338cd062a5ca34eb37f3ed7d35c1457a2fea
if {(exists file "jre/bin/java.exe" of folder "__GTS" of storage folder of client or exists file "jre/bin/java" of folder "__GTS" of storage folder of client)}	
	move __appendfile "{storage folder of client}/__GTS/jre/sha256.txt"
else
	delete __appendfile
endif

// clean up the work folder
folder delete "{storage folder of client}/__GTS/Temp"
			

// Last Change
appendfile {now as string}

// Version
appendfile {parameter "version"}

// Build ID
appendfile {parameter "build_id"}

// create the version file
delete "{storage folder of client}/__GTS/version.txt"
move __appendfile "{storage folder of client}/__GTS/version.txt"

// append JRE version to end
appendfile "{storage folder of client}\__GTS\jre\bin\java.exe" -version >> "{storage folder of client}\__GTS\version.txt" 2>&1 
delete launch.bat
move __appendfile launch.bat
wait launch.bat

// HCLauncher version
appendfile "{storage folder of client}\__GTS\jre\bin\java.exe" -jar {parameter "dump"} "{storage folder of client}\__GTS\HCLauncher_400\HCLauncher.jar" -version >> "{storage folder of client}\__GTS\HCLauncher_400\version.txt" 2>&1 
delete launch.bat
move __appendfile launch.bat
wait launch.bat

			
			]]></ActionScript>
			<SuccessCriteria Option="RunToCompletion"></SuccessCriteria>
		</DefaultAction>
	</Fixlet>
</BES>
