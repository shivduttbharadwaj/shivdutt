<?xml version="1.0" encoding="UTF-8"?>
<BES xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="BES.xsd">
	<Fixlet>
		<Title>Updated Windows Installation Folders - IBM BigFix version 9.5.11 Now Available!</Title>
		<Description><![CDATA[
<DIV class=FixletMessage>
<TABLE><TBODY><TR><TD><b>Version 9.5.11 of IBM BigFix is now available! <BR></b><BR>The following list of computers have outdated IBM BigFix Installers, created by the Installation Generator during initial installation. It is recommended that you upgrade these installation folders to install IBM BigFix 9.5 components so that all further installations of the IBM BigFix Server, Client, and Console will be the latest version.</TD></TR></TBODY></TABLE>
</DIV>
<DIV class=FixletProperty>
<H4>File Size:</H4>
427 MB
 </DIV>
]]></Description>
		<Relevance>(if exists property "in proxy agent context" then ( not in proxy agent context ) else true )</Relevance>
		<Relevance>windows of operating system</Relevance>
		<Relevance><![CDATA[version of registration server >= "9.2.3"]]></Relevance>
		<Relevance><![CDATA[((windows of it)AND(major version of it > 6 OR((major version of it = 6)AND((minor version of it >= 1)OR(service pack major version of it >= 2))))AND(platform id of it != 3)AND(architecture of it = "x86_64"))of operating system]]></Relevance>
		<Relevance><![CDATA[((not exists(value "Version" of key "HKLM\SOFTWARE\BigFix\BES Installation Generator" of it)whose(it as string as version >= "9.5.11.0"))OR(exists file "setup.exe" whose(version of it < "9.5.11.191")of folder "Server" of(value "InstallFolder" of key "HKLM\SOFTWARE\BigFix\BES Installation Generator" of it as folder))OR(exists file "setup.exe" whose(version of it < "9.5.11.191")of folder "Console" of(value "InstallFolder" of key "HKLM\SOFTWARE\BigFix\BES Installation Generator" of it as folder))OR(exists file "setup.exe" whose(version of it < "9.5.11.191")of folder "Client" of(value "InstallFolder" of key "HKLM\SOFTWARE\BigFix\BES Installation Generator" of it as folder)))of registry]]></Relevance>
		<Relevance>evaluation of client license = false</Relevance>
		<Relevance>if exists property "in agent context" then (in agent context) else true</Relevance>
		<Category>Upgrade</Category>
		<DownloadSize>448241257</DownloadSize>
		<Source>IBM</Source>
		<SourceID><![CDATA[<Unspecified>]]></SourceID>
		<SourceReleaseDate>2018-12-18</SourceReleaseDate>
		<SourceSeverity>Critical</SourceSeverity>
		<MIMEField>
			<Name>x-fixlet-first-propagation</Name>
			<Value>Tue, 18 Dec 2018 16:03:16 +0000</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-domain_attributes</Name>
			<Value>BES Deploy Install</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-modification-time</Name>
			<Value>Tue, 18 Dec 2018 16:03:16 +0000</Value>
		</MIMEField>
		<Delay>PT1S</Delay>
		<DefaultAction ID="Action1">
			<Description>
				<PreLink>Click </PreLink>
				<Link>here</Link>
				<PostLink> to upgrade your IBM BigFix Installation folders.</PostLink>
			</Description>
			<ActionScript MIMEType="application/x-Fixlet-Windows-Shell"><![CDATA[folder delete "{(client folder of current site as string) & "\__Local\Upgrade"}"
// If the next line fails, there is not enough free space to complete this action
continue if {((if (exists variable "temp" of it) then (230804344 < free space of drive of folder(value of variable "temp" of it)) else true) of environment) AND (230804344 < free space of drive of parent folder of client)}
folder create "{(client folder of current site as string) & "\__Local\Upgrade"}"
// Update the IBM BigFix Installation Folders if installed
if {(exists key "HKLM\SOFTWARE\BigFix\BES Installation Generator" of registry)}
	// Update the IBM BigFix Installation Folders if installed
	if {not exists(value "Version" of key "HKLM\SOFTWARE\BigFix\BES Installation Generator" of registry)whose((it >= "9.5.11.0")of(it as string as version))}
		prefetch BESFolders.exe sha1:7cec5ee3e970a2d2a8f24ef4b358f8427e613f01 size:230804344 http://software.bigfix.com/download/bes/95/BigFix-BES-9.5.11.191.exe sha256:e194727e0785c44e528947b01c9e4fbdc15d74689ba528377deda56ef4fb87e6
		regset "[HKEY_LOCAL_MACHINE\Software\Policies\Microsoft\Windows\Installer]" "Logging"="voicewarmupx"
		move __Download\BESFolders.exe __Local\Upgrade\BESFolders.exe
		override wait
			completion=job
		wait __Local\Upgrade\BESFolders.exe /s /Verbose"C:\besinstallationgeneratorupgrade.log" /v"REBOOT=ReallySuppress MSIRESTARTMANAGERCONTROL=Disable"
		delete __Local\Upgrade\BESFolders.exe
		action may require restart
	else
		parameter "installFolder"="{value "InstallFolder" of key "HKLM\SOFTWARE\BigFix\BES Installation Generator" of registry}"
		if {parameter "installFolder" ends with "\"}
			parameter "installLocation"="{preceding text of last "\" of parameter "installFolder"}"
		else
			parameter "installLocation"="{parameter "installFolder"}"
		endif
		prefetch unzip.exe sha1:84debf12767785cd9b43811022407de7413beb6f size:204800 http://software.bigfix.com/download/redist/unzip-6.0.exe sha256:2122557d350fd1c59fb0ef32125330bde673e9331eb9371b454c2ad2d82091ac
		if {exists file "setup.exe" whose(version of it < "9.5.11.191") of folder "Server" of folder(parameter "installLocation")}
			prefetch BESServerUpgrade.exe sha1:208c9ef009562e9b6a9e78b96160f39af4b00214 size:83146160 http://software.bigfix.com/download/bes/95/BigFix-BES-Server-9.5.11.191.exe sha256:e86a52631894b2442f2acff2f94aa095fa755ffe26413cef277c3466b8278d76
			parameter "serverLocation"="{parameter "installLocation"}\Server\"
			delete "{parameter "serverLocation"}ActionSite.afxm"
			delete "{parameter "serverLocation"}masthead.afxm"
			copy "{pathname of parent folder of parent folder of client folder of current site}\ActionSite.afxm" "{parameter "serverLocation"}masthead.afxm"
			delete "{parameter "serverLocation"}setup.exe"
			copy __Download\BESServerUpgrade.exe "{parameter "serverLocation"}setup.exe"
		endif
		if {exists file "setup.exe" whose(version of it < "9.5.11.191") of folder "Console" of folder(parameter "installLocation")}
			prefetch BESConsoleUpgrade.exe sha1:431d4ecb6d960f450a217440f747d8027006a931 size:51353072 http://software.bigfix.com/download/bes/95/BigFix-BES-Console-9.5.11.191.exe sha256:b601184f5d7fcd0ca62a9f312d3e7efd0253830e7c2e47dbf756113f09b5fa1e
			parameter "consoleLocation"="{parameter "installLocation"}\Console\"
			delete "{parameter "consoleLocation"}ActionSite.afxm"
			delete "{parameter "consoleLocation"}masthead.afxm"
			copy "{pathname of parent folder of parent folder of client folder of current site}\ActionSite.afxm" "{parameter "consoleLocation"}masthead.afxm"
			delete "{parameter "consoleLocation"}setup.exe"
			copy __Download\BESConsoleUpgrade.exe "{parameter "consoleLocation"}setup.exe"
			parameter "cdtFolder"="{parameter "installLocation"}\ClientDeployTool"
			parameter "cdtLocation"="{parameter "cdtFolder"}\BigFixClientDeploy.msi"
			if {exists file (parameter "cdtLocation")}
				prefetch BESCDTMSIBundle.zip sha1:cbbad6662afc7059fb3c1fe7ae9b25b1f0632e21 size:26519936 http://software.bigfix.com/download/bes/95/BigFix-BES-CDTMSIBundle-9.5.11.191.zip sha256:16c9e44ef78d2cb55077f1d3f3506144b78aeaefe333076fe2e84158dd2c1de5
				// Unzip the file overwriting existing files
				waithidden __Download\unzip.exe -qq -o -d "{parameter "cdtFolder"}" __Download\BESCDTMSIBundle.zip
			endif
		endif
		if {exists file "setup.exe" whose(version of it < "9.5.11.191") of folder "Client" of folder(parameter "installLocation")}
			prefetch BESClientUpgrade.exe sha1:c5b7813b864e38fc1f950a49512ad962e62de2b4 size:16903472 http://software.bigfix.com/download/bes/95/BigFix-BES-Client-9.5.11.191.exe sha256:28d5670aa6c2e7920c42177494f36e78e5fb351b8661fcf122db5e4d2121f487
			parameter "clientLocation"="{parameter "installLocation"}\Client\"
			delete "{parameter "clientLocation"}ActionSite.afxm"
			delete "{parameter "clientLocation"}masthead.afxm"
			copy "{pathname of parent folder of parent folder of client folder of current site}\ActionSite.afxm" "{parameter "clientLocation"}masthead.afxm"
			delete "{parameter "clientLocation"}setup.exe"
			copy __Download\BESClientUpgrade.exe "{parameter "clientLocation"}setup.exe"
		endif
		if {exists file "BESClientSetupMSI.exe" whose(version of it < "9.5.11.191") of folder "ClientMSI" of folder(parameter "installLocation")}
			prefetch BESClientMSIBundle.zip sha1:8a20104d23470f18683b8ae6c72695b2f664a5e1 size:13075121 http://software.bigfix.com/download/bes/95/BigFix-BES-ClientMSIBundle-9.5.11.191.zip sha256:c2bd7398e35a5475366d9c33c5652bca81ac69bd762cba26b7f991c34d2839ad
			parameter "clientMSILocation"="{parameter "installLocation"}\ClientMSI\"
			delete "{parameter "clientMSILocation"}ActionSite.afxm"
			delete "{parameter "clientMSILocation"}masthead.afxm"
			copy "{pathname of parent folder of parent folder of client folder of current site}\ActionSite.afxm" "{parameter "clientMSILocation"}masthead.afxm"
			// Unzip the file overwriting existing files
			waithidden __Download\unzip.exe -qq -o -d "{preceding text of last "\" of parameter "clientMSILocation"}" __Download\BESClientMSIBundle.zip
			override wait
				completion=job
			wait "{parameter "clientMSILocation"}BESClientSetupMSI.exe" "{parameter "clientMSILocation"}masthead.afxm" "{parameter "clientMSILocation"}BigFixAgent.msi" /silent
		endif
		parameter "webuiSetupLocation"="{parameter "installLocation"}\WebUI\"
		parameter "webuiSetupFile" = "{parameter "webuiSetupLocation"}setup.exe"
		if {exists file (parameter "webuiSetupFile") whose(version of it < "9.5.11.191")}
			prefetch BESWebUIUpgrade.exe sha1:7caf38ad0e4963120b2e7cf77b307e52834233f6 size:26439152 http://software.bigfix.com/download/bes/95/BigFix-BES-WebUI-9.5.11.191.exe sha256:0321fedff12e22e601c85671bb83d15903a5b55fbd99a9a2154b87e4366124d1
			delete "{parameter "webuiSetupFile"}"
			copy __Download\BESWebUIUpgrade.exe "{parameter "webuiSetupFile"}"
		endif
	endif
endif
folder delete "{(client folder of current site as string) & "\__Local\Upgrade"}"]]></ActionScript>
		</DefaultAction>
		<Action ID="Link2">
			<Description>
				<PreLink>Click </PreLink>
				<Link>here</Link>
				<PostLink> for manual upgrade instructions.</PostLink>
			</Description>
			<ActionScript MIMEType="text/x-uri">http://support.bigfix.com/bes/install/upgradebes.html</ActionScript>
		</Action>
	</Fixlet>
</BES>
