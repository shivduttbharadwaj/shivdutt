<?xml version="1.0" encoding="UTF-8"?>
<BES xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="BES.xsd">
	<Fixlet>
		<Title>Enable Encryption for Clients</Title>
		<Description><![CDATA[
<DIV class=FixletMessage>
<TABLE><TBODY><TR><TD> <P>This task will deploy Crypto Utility to the BES Client folder, and generate a set of public and private key pair. These keys can be used to securely set passwords on the client machines.</P><P>Please note that only certain Dashboards will use this key pair for encryption.</P><BR> <P>Use the <?relevance link of fixlets whose(id of it = 977) of current bes site ?> to view the public keys reported by endpoints with encryption. </P></TD></TR></TBODY></TABLE>
</DIV>
<DIV class=FixletProperty>
<H4>File Size:</H4>
441 KB
 </DIV>
]]></Description>
		<Relevance>not exists folder "BFEncrypt" whose (exists file "private.pem" of it) of parent folder of client or not exists folder "BFEncrypt" whose (exists file "public.cert" of it) of parent folder of client</Relevance>
		<Relevance>name of operating system starts with "Win"</Relevance>
		<Category>Setup</Category>
		<DownloadSize>451072</DownloadSize>
		<Source>BigFix</Source>
		<SourceID><![CDATA[<Unspecified>]]></SourceID>
		<SourceReleaseDate>2012-06-28</SourceReleaseDate>
		<SourceSeverity><![CDATA[<Unspecified>]]></SourceSeverity>
		<MIMEField>
			<Name>x-fixlet-first-propagation</Name>
			<Value>Thu, 28 Jun 2012 20:58:46 +0000</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-modification-time</Name>
			<Value>Tue, 14 Mar 2017 13:40:46 +0000</Value>
		</MIMEField>
		<Delay>PT1S</Delay>
		<Action ID="Action1">
			<Description>
				<PreLink>Click </PreLink>
				<Link>here</Link>
				<PostLink> to enable encryption for BigFix Clients.</PostLink>
			</Description>
			<ActionScript MIMEType="application/x-Fixlet-Windows-Shell"><![CDATA[parameter "oldCryptoFolderPath" = "{pathname of parent folder of client}\openSSL"
parameter "oldCryptoFilePath" = "{parameter "oldCryptoFolderPath"}\AppEngUtility.dll"
parameter "cryptoFolderPath" = "{pathname of parent folder of client}\BFEncrypt"
parameter "cryptoFilePath" = "{parameter "cryptoFolderPath"}\AppEngUtility.dll"
parameter "cryptoRegKey" = "HKEY_LOCAL_MACHINE\Software\Bigfix\EnterpriseClient\EncryptionControl"

// unregister old CryptoUtility.dll
if {exists file (parameter "oldCryptoFilePath")}
	waithidden "{pathname of system folder}\cmd.exe" /C regsvr32.exe /s /u "{parameter "oldCryptoFilePath"}" 
endif

// Create Folder if needed
if {not exists folder (parameter "cryptoFolderPath")}
	waithidden "{pathname of system folder}\cmd.exe" /C mkdir "{parameter "cryptoFolderPath"}"
endif

// Need to add version check here for next version
if {not exists folder (parameter "cryptoFolderPath") whose (exists file "AppEngUtility.dll" whose (version of it >= "9.0.10115.0") of it)}
	waithidden "{pathname of system folder}\cmd.exe" /C regsvr32.exe /s /u "{parameter "cryptoFilePath"}"
	prefetch AppEngUtility.dll sha1:6e8349f608c62e80bb13cafc9b006e16f4126080 size:604608 http://software.bigfix.com/download/bes/mdm/AppEngUtility-9.0.10115.0.dll sha256:8540200a2361db31cdf0414cc1ad70ca1ab1dce04ad6bf7b4820c5c1cce28ca9
	if{exists running services whose (service name of it = "BESPluginService")}
		waithidden "{pathname of system folder}\cmd.exe" /C net stop BESPluginService
		delete "{parameter "cryptoFilePath"}"
		move __Download\AppEngUtility.dll "{parameter "cryptoFilePath"}"	
		waithidden "{pathname of system folder}\cmd.exe" /C net start BESPluginService
	else
		delete "{parameter "cryptoFilePath"}"
		move __Download\AppEngUtility.dll "{parameter "cryptoFilePath"}"	
	endif
endif
if {not exists folder (parameter "cryptoFolderPath") whose (exists file "libBEScrypto_1_0_0_1.dll" of it)}
	prefetch libBEScrypto_1_0_0_1.dll sha1:80e9e5d26070f95249f7575d0d5b9cb66e6d5c02 size:1154120 http://software.bigfix.com/download/bes/util/libBEScrypto_1_0_0_1.dll sha256:e7b3d3625c468b6954bcabe758c49ac45cc0f40e18022a1d0523486e9e4d5462
	delete "{parameter "cryptoFolderPath"}\libBEScrypto_1_0_0_1.dll"
	move __Download\libBEScrypto_1_0_0_1.dll "{parameter "cryptoFolderPath"}\libBEScrypto_1_0_0_1.dll"
endif
if {not exists folder (parameter "cryptoFolderPath") whose (exists file "libBESssl_1_0_0_1.dll" of it)}
	prefetch libBESssl_1_0_0_1.dll sha1:34e3099090fa5eb2d797822bb8bf8a53ead254ee size:273480 http://software.bigfix.com/download/bes/util/libBESssl_1_0_0_1.dll sha256:35d0fd3bab4a1790ddb1e87a3b8903a0d428de703610f2bebb8aa9008b209330
	delete "{parameter "cryptoFolderPath"}\libBESssl_1_0_0_1.dll"
	move __Download\libBESssl_1_0_0_1.dll "{parameter "cryptoFolderPath"}\libBESssl_1_0_0_1.dll"
endif


// set public/private key path
regset "[{parameter "cryptoRegKey"}]" "CertificatePath"="{escape of (parameter "cryptoFolderPath")}\\public.cert"
regset "[{parameter "cryptoRegKey"}]" "PrivateKeyPath"="{escape of (parameter "cryptoFolderPath")}\\private.pem"

// register CryptoUtility.dll
waithidden "{pathname of system folder}\cmd.exe" /C regsvr32.exe /s "{parameter "cryptoFilePath"}" 
waithidden "{pathname of system folder}\cmd.exe" /C regsvr32.exe /s "{parameter "cryptoFilePath"}" 

// Make sure the dll has been registered by checking registry key
continue if {exists key "HKEY_LOCAL_MACHINE\SOFTWARE\Classes\CLSID\%7BC5A43980-AD5F-44BE-9C2B-B21B2848C660%7D" whose (exist key "InprocServer32" whose (exists value whose (it as string = (parameter "cryptoFilePath")) of it) of it) of x32 registry}

// create initCrypto.vbs and run it to create public/private keys
createfile until __the_end
option explicit
dim crypto, shell
set crypto=createobject("AppEngUtility.Crypto")
'Only need to call it once during setup
crypto.InitializeKeyPairAndCert
__the_end

delete "{parameter "cryptoFolderPath"}\initCrypto.vbs"
move __createfile "{parameter "cryptoFolderPath"}\initCrypto.vbs"
if {not exists folder (parameter "cryptoFolderPath") whose (exists file "private.pem" of it) OR not exists folder (parameter "cryptoFolderPath") whose (exists file "public.cert" of it)}	
	createfile until __END__
	cd /D "{parameter "cryptoFolderPath"}"
	cscript /B initCrypto.vbs
	__END__
	delete runInitCrypto.bat
	move __createfile runInitCrypto.bat
	waithidden runInitCrypto.bat
endif

// Make sure private and public key are created
continue if {exists key "HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\EnterpriseClient\EncryptionControl" whose (exists value "CertificatePath" whose (exists file (it as string)) of it AND exists value "PrivateKeyPath" whose (exists file (it as string)) of it) of x32 registry}

// Set Registry key
regset "[{parameter "cryptoRegKey"}]" "InstalledTime"="{now}"

]]></ActionScript>
		</Action>
	</Fixlet>
</BES>
