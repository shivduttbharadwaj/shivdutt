<?xml version="1.0" encoding="UTF-8"?>
<BES xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="BES.xsd">
	<Fixlet>
		<Title>TROUBLESHOOTING:Run IEM Server Diagnostic</Title>
		<Description><![CDATA[
<TABLE><TBODY><TR><TD><P>The Server Diagnostic Tool is a utitlity that gather various statistics and files for server analysis.</P> <P>This tool will&nbsp;bundle the following using BFArchive:</P> <UL> <LI>ActionSite.afxm <LI>BESRelay.log <LI>ClientRegisterData\registrationlist.txt <LI>ClientRegisterData\ReplicationServerList.xml <LI>FillDBData\FillDB.log <LI>GatherDBData\GatherDB.log <LI>Mirror Server\Download.log <LI>Mirror Server\Config\DownloadPlugins.db <LI>Mirror Server\Config\DownloadState.db <LI>Mirror Server\Inbox\bfemapfile.xml <LI>Mirror Server\Inbox\GatherState.xml <LI>Server audit logging.<LI>Create a small informational log containing useful information about the IEM Server.</LI></UL> <P>The output will be saved in "Server Diagnostics" under the "BES Server" folder, as a timestamped .bfarchive file.</P><P>NOTE: DOS windows may appear during this task on Windows machines.</P></TD></TR></TBODY></TABLE>
]]></Description>
		<Relevance>exists relay service OR exists main gather service</Relevance>
		<Relevance>exists main gather service</Relevance>
		<Category></Category>
		<DownloadSize>0</DownloadSize>
		<Source>Internal</Source>
		<SourceID></SourceID>
		<SourceReleaseDate>2011-06-17</SourceReleaseDate>
		<SourceSeverity></SourceSeverity>
		<CVENames></CVENames>
		<SANSID></SANSID>
		<MIMEField>
			<Name>x-fixlet-first-propagation</Name>
			<Value>Wed, 30 Jan 2013 23:21:23 +0000</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-modification-time</Name>
			<Value>Sat, 11 Mar 2017 00:28:25 +0000</Value>
		</MIMEField>
		<Delay>PT1S</Delay>
		<DefaultAction ID="Action1">
			<Description>
				<PreLink>Click </PreLink>
				<Link>here</Link>
				<PostLink> to run the Server Diagnostic tool and save the results.</PostLink>
			</Description>
			<ActionScript MIMEType="application/x-Fixlet-Windows-Shell"><![CDATA[if {windows of operating system}
	prefetch BFArchive.exe sha1:5ceb34639963efd1f8f405336e584e7d02469dc1 size:399872 http://software.bigfix.com/download/bes/util/BFArchive-8.0.0.0.exe sha256:83418794467ca8abf2fa6bf8da94a91ecfaa7b8dacd23c899473a6cbb2753801
	parameter "ServerFolder"="{value "StoragePath" of key "HKLM\Software\BigFix\Enterprise Server\GlobalOptions" of registry}"
else
	prefetch BFArchive.exe sha1:2c8a26124ccefe0b04a2aa0dc4015bf718033c3f size:375900 http://software.bigfix.com/download/bes/util/bfarchive.i686_rhe5 sha256:c9762a08170375fc399bc21dbc0d2f0f025cada43be7b364598082cb03304f36
	wait chmod +x __Download/BFArchive.exe
	parameter "ServerFolder"="{key ("StoragePath") of section ("Software\BigFix\Enterprise Server\GlobalOptions") of file "/var/opt/BESServer/besserver.config"}"
endif
//remove existing folder and create one anew
//this prevents failure of the fixlet due to existing files
folder delete "{parameter "ServerFolder" & "/Server Diagnostics/ServerDiagTemp"}"
folder create "{parameter "ServerFolder" & "/Server Diagnostics"}"
folder create "{parameter "ServerFolder" & "/Server Diagnostics/ServerDiagTemp"}"

//diagnostic log creation
delete __createfile

//if windows
if {windows of operating system}
	createfile until __DONE
"Operating System"
{operating system}

"RAM"
{("Used: " & (used amount of it / (1024 * 1024)) as string & "MB, Free: " & (free amount of it / (1024 * 1024)) as string & "MB, Total: " & (total amount of it / (1024 * 1024)) as string & "MB") of ram}

"Free Disk Space\Total Space of installation drive"
{((free space of it / 1024 / 1024) as string & "MB Free", (total space of it / 1024 /1024) as string & "MB Total") of drive of file of main gather service}

"Free Space\Total Space of drive containing wwwroot"
{((free space of it / 1024 / 1024) as string & "MB Free", (total space of it / 1024 /1024) as string & "MB Total") of drive of folder (value "wwwRootFolder" of key "HKEY_LOCAL_MACHINE\Software\Bigfix\Enterprise Server" of registry as string)}

"Number of files in the FillDB Buffer"
{number of files of folder (value "DataDirectoryPath" of key "HKEY_LOCAL_MACHINE\Software\Bigfix\Enterprise Server\FillDB" of registry as string)}

"Page File Usage"
{if (windows of operating system)then("Usage: " & (page file usage of it / (1024 * 1024)) as string & "MB, Peak: " & (peak page file usage of it / (1024 * 1024)) as string & "MB")of processes whose (name of it is "BESClient.exe") else "N/A"}

"Running Services"
{(it, "%0A") of running services as string}

"Client settings"
{(it, "%0A") of settings of client as string}
	__DONE
else
//if linux
	createfile until __DONE
"Operating System"
{operating system}

"RAM"
{("Used: " & (used amount of it / (1024 * 1024)) as string & "MB, Free: " & (free amount of it / (1024 * 1024)) as string & "MB, Total: " & (total amount of it / (1024 * 1024)) as string & "MB") of ram}

"Free Disk Space\Total Space of installation drive"
{((free space of it / 1024 / 1024) as string & "MB Free", (total space of it / 1024 /1024) as string & "MB Total") of drive of folder "/var/opt/BESServer"}

"Free Space\Total Space of drive containing wwwroot"
{((free space of it / 1024 / 1024) as string & "MB Free", (total space of it / 1024 /1024) as string & "MB Total") of drive of folder (key ("wwwRootFolder") of section ("Software\BigFix\Enterprise Server") of file "besserver.config" of parent folder of folder (value of setting "_BESRelay_HTTPServer_ServerRootPath" of client))} 

"Number of files in the FillDB Buffer"
{number of files of folder (key ("BufferDir") of section ("Software\BigFix\Enterprise Server\FillDB") of file "besserver.config" of parent folder of folder (value of setting "_BESRelay_HTTPServer_ServerRootPath" of client))} 

"System Locale"
{system locale}

"Client settings"
{(it, "%0A") of settings of client as string}
	__DONE
endif

move __createfile "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/diagnostic.log

//============Insert Selected Files into Temp Folder==========

//files from BES Server
if {exists file (parameter "ServerFolder" as string & "/Actionsite.afxm")}
	copy "{parameter "ServerFolder"}/actionsite.afxm"  "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/Actionsite.afxm"
endif
if {exists file (parameter "ServerFolder" as string & "/BESRelay.log")}
	copy "{parameter "ServerFolder"}/BESRelay.log"  "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/BESRelay.log"
endif
if {exists file (parameter "ServerFolder" as string & "/ClientRegisterData/registrationlist.txt")}
	copy "{parameter "ServerFolder"}/ClientRegisterData/registrationlist.txt"  "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/registrationlist.txt"
endif
if {exists file (parameter "ServerFolder" as string & "/ClientRegisterData/ReplicationServerList.xml")}
	copy "{parameter "ServerFolder"}/ClientRegisterData/ReplicationServerList.xml"  "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/ReplicationServerList.xml"
endif
if {exists file (parameter "ServerFolder" as string & "/GatherDBData/GatherDB.log")}
	copy "{parameter "ServerFolder"}/GatherDBData/GatherDB.log"  "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/GatherDB.log"
endif
if {exists file (parameter "ServerFolder" as string & "/Mirror Server/Download.log")}
	copy "{parameter "ServerFolder"}/Mirror Server/Download.log" "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/Download.log"
endif
if {exists file (parameter "ServerFolder" as string & "/Mirror Server/config/DownloadPlugins.db")}
	copy "{parameter "ServerFolder"}/Mirror Server/config/DownloadPlugins.db"  "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/DownloadPlugins.db"
endif
if {exists file (parameter "ServerFolder" as string & "/Mirror Server/config/DownloadState.db")}
	copy "{parameter "ServerFolder"}/Mirror Server/config/DownloadState.db" "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/DownloadState.db"
endif
if {exists file (parameter "ServerFolder" as string & "/Mirror Server/inbox/bfemapfile.xml")}
	copy "{parameter "ServerFolder"}/Mirror Server/inbox/bfemapfile.xml" "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/bfemapfile.xml"
endif
if {exists file (parameter "ServerFolder" as string & "/Mirror Server/inbox/GatherState.xml")}
	copy "{parameter "ServerFolder"}/Mirror Server/inbox/GatherState.xml" "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/GatherState.xml"
endif
if {exists file (parameter "ServerFolder" as string & "/server_audit.log")}
	copy "{parameter "ServerFolder"}/server_audit.log" "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/server_audit.log"
endif
//FillDBdata can be configured to reside in different places,
//we have to check for them
if {windows of operating system}
	copy "{(value "DebugOut" of key "HKEY_LOCAL_MACHINE\Software\Bigfix\Enterprise Server\FillDB" of registry as string)}" "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/FillDB.log"
else
	copy "{(key ("DebugOut") of section ("Software\BigFix\Enterprise Server\FillDB") of file "besserver.config" of parent folder of folder (value of setting "_BESRelay_HTTPServer_ServerRootPath" of client))}" "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp/FillDB.log"
endif
//=====Compression using BFArchive=======
wait "__Download/BFArchive.exe" -a "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp" "{parameter "ServerFolder"}/Server Diagnostics/ServerDiag-{current year as string & "-" & current month as two digits & "-" & current day_of_month as string & "-" & two digit hour of current time_of_day as string & two digit minute of current time_of_day as string & ".bfarchive"}"



//delete temp folder
folder delete "{parameter "ServerFolder"}/Server Diagnostics/ServerDiagTemp"
]]></ActionScript>
			<SuccessCriteria Option="RunToCompletion"></SuccessCriteria>
		</DefaultAction>
	</Fixlet>
</BES>
