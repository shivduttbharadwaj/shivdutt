<?xml version="1.0" encoding="UTF-8"?>
<BES xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="BES.xsd">
	<Fixlet>
		<Title>Upgrade BES Client Logging Service</Title>
		<Description><![CDATA[
<DIV class=FixletMessage>
<TABLE><TBODY><TR><TD> <P>BigFix has released an upgrade to the BES Client Logging Service. This upgrade contains several bug fixes and&nbsp;BigFix highly recommends&nbsp;that all customers apply the upgrade. This upgrade is required to upgrade the BES Client to version 7.0.9 or greater if any BigFix Extensions are installed.</P> <P>Use the action below to upgrade to the latest version of the BES Client Logging Service.</P> <P><STRONG>Note:</STRONG> This action will temporarily stop the BES Client Logging Service. Please schedule the upgrade to occur at a time when a service interruption is acceptable.</P> <P>The following BigFix Extensions require the BES Client Logging Service:</P> <UL> <LI>AntiPest 2 <LI>AntiVirus <LI>DLP <LI>Firewall <LI>Power Management (on 7.2 clients)</LI></UL></TD></TR></TBODY></TABLE>
</DIV>
<DIV class=FixletProperty>
<H4>File Size:</H4>
3.18 MB+
 </DIV>
]]></Description>
		<Relevance>name of operating system as lowercase starts with "win" </Relevance>
		<Relevance><![CDATA[version of client >= "6"]]></Relevance>
		<Relevance><![CDATA[exists service "BESClientLoggingService" whose (version of file of it < "7.1.0.15")]]></Relevance>
		<Category>Upgrade</Category>
		<DownloadSize>3338991</DownloadSize>
		<Source>BigFix</Source>
		<SourceID><![CDATA[<Unspecified>]]></SourceID>
		<SourceReleaseDate>2008-02-26</SourceReleaseDate>
		<SourceSeverity>Critical</SourceSeverity>
		<MIMEField>
			<Name>x-fixlet-first-propagation</Name>
			<Value>Fri, 09 Nov 2007 18:40:08 +0000</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-domain_attributes</Name>
			<Value>BES Deploy Client</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-modification-time</Name>
			<Value>Mon, 06 Jan 2014 19:12:31 +0000</Value>
		</MIMEField>
		<Delay>PT1S</Delay>
		<DefaultAction ID="Action1">
			<Description>
				<PreLink>Click </PreLink>
				<Link>here</Link>
				<PostLink> to initiate the deployment process.</PostLink>
			</Description>
			<ActionScript MIMEType="application/x-Fixlet-Windows-Shell"><![CDATA[//*********************** RUN UPGRADE *******************//

prefetch BESClientLoggingServiceSetup.exe sha1:31d7160c8a10aeda6b212bf1360802311503a842 size:3338991 http://software.bigfix.com/download/LoggingService/BESClientLoggingServiceSetup-7.1.0.15.exe sha256:1f5e23eac40d4b3954dc88443c005b73fd804602a0c452a389a8e4296eef0846

wait __Download\BESClientLoggingServiceSetup.exe /s /v"/qn REBOOT=REALLYSUPPRESS"


//*********************** IF LOG SVC EXISTS, STOP IT *******************//

if {(exists folder (pathname of parent folder of regapp "besclient.exe" & "\BESClientLoggingService") whose (exists file whose (name of it = "BESClientLoggingService.exe") of it))}

waithidden {(pathname of parent folder of regapp "besclient.exe" & "\BESClientLoggingService\BESClientLoggingService.exe -e")}

endif

continue if {not exists running service "BESClientLoggingService"}


//*********************** DELETE OLD PLUGIN STRINGS *******************//

if {exists key "HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\LoggingService\Plugins\Locations" whose (exists name whose (it = "antipest" or it = "antivirus" or it = "compliance" or it = "firewall" or it = "dlp") of values of it) of registry}

delete regremove.reg
delete __appendfile
appendfile REGEDIT4
appendfile [HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\LoggingService\Plugins\Locations]
appendfile "antipest"=-
appendfile "antivirus"=-
appendfile "compliance"=-
appendfile "firewall"=-
appendfile "dlp"=-

copy __appendfile regremove.reg
waithidden regedit /s regremove.reg

endif


//*********************** DOWNLOAD NECESSARY PLUGINS AND REGISTER THEM *******************//

// Create Lib folder if it does not exist
if {not exists folder (pathname of parent folder of regapp "besclient.exe" & "\BESClientLoggingService") whose (exists folder "Lib" of it)}

delete __appendfile 
delete myScript.bat 
appendfile @ECHO OFF 
appendfile mkdir "{(pathname of parent folder of regapp "besclient.exe" & "\BESClientLoggingService\Lib")}" > NUL 2> NUL 
copy __appendfile myScript.bat 
wait "{pathname of client folder of site "BESSupport"}\RunQuiet.exe" myScript.bat
delete myScript.bat
endif


// #AP2
if {((exist regapp "BigFixAntiPest.exe" whose (version of it >= "0.9.0.34")))}

prefetch "antipest.dll" sha1:552946f80235b9f6ad8444909424d554bad555f7 size:102400 http://software.bigfix.com/download/LoggingService/lib/antipest.dll sha256:a0a21b70c653d900a4748232edaabd8ad71e02a1dc1c314922b25e424f2e4977

delete "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\antipest.dll"

copy "__Download\antipest.dll" "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\antipest.dll"

regset "[HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\LoggingService\Plugins\Locations]" "CA_AP"="{escape of (pathname of parent folder of file of service "BESClientLoggingService" & "\lib\antipest.dll")}"

endif

// #AV
if {((if (exists key "HKEY_LOCAL_MACHINE\SOFTWARE\ComputerAssociates\eTrustAntivirus\CurrentVersion\Path" whose (exists value "HOME" of it) of registry) then (exists file ((value "HOME" of key "HKEY_LOCAL_MACHINE\SOFTWARE\ComputerAssociates\eTrustAntivirus\CurrentVersion\Path" of registry as string) & "\InoOEM.dll") whose (version of it = "7.1")) else (false)) AND (exists key "HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\AntiVirus" of registry))}

prefetch "antivirus.dll" sha1:7462e0790198d6f2afbe9c79db589e00eeb8d4c6 size:167936 http://software.bigfix.com/download/LoggingService/lib/antivirus.dll sha256:765537620d0ef13df20caea4f7db195f62f08ffea7b0e52e84e8794030a89018

prefetch "eavrtsdk.dll" sha1:d4e2a29a1d9891e72add0fcb7e47da4e3a57e608 size:241664 http://software.bigfix.com/download/LoggingService/lib/eavrtsdk.dll sha256:f3ee2705b7c72d2c433ca824662ca61f79607908f04f97c9743e1575bf5654d4

delete "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\antivirus.dll"
delete "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\eavrtsdk.dll"

copy "__Download\antivirus.dll" "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\antivirus.dll"
copy "__Download\eavrtsdk.dll" "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\eavrtsdk.dll"

regset "[HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\LoggingService\Plugins\Locations]" "CA_AV"="{escape of (pathname of parent folder of file of service "BESClientLoggingService" & "\lib\antivirus.dll")}"

regset "[HKEY_LOCAL_MACHINE\Software\BigFix\AntiVirus]" "SDKPath"="{escape of (pathname of parent folder of file of service "BESClientLoggingService" & "\lib")}"

endif

// #DLP
if {(((exists key "HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\DLP" whose (exists folder (values "installedPath" of it as string) whose (exists file "BESLeakProofCTRL.exe" whose (version of it >= "1.1.0.17") of it)) of registry)))}

prefetch "dlp.dll" sha1:e5d05be8daedf5143d2ae2b10fe75cc834ae0bad size:110592 http://software.bigfix.com/download/LoggingService/lib/dlp.dll sha256:1655c23983a66642d459f3cbd5f53faf37a3fc1e364d761020f7580b6ab958a7

delete "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\dlp.dll"

copy "__Download\dlp.dll" "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\dlp.dll"

regset "[HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\LoggingService\Plugins\Locations]" "TM_DLP"="{escape of (pathname of parent folder of file of service "BESClientLoggingService" & "\lib\dlp.dll")}"

endif

// #FW
if {((exists key "HKEY_LOCAL_MACHINE\SOFTWARE\CA\HIPSEngine\Products\BigFixFW" whose (exists value "UninstallCmdLine" of it) of registry))}

prefetch "compliance.dll" sha1:ba6b4f295a8933584d9c502db54797d839a4ee81 size:151552 http://software.bigfix.com/download/LoggingService/lib/compliance.dll sha256:099f516812ad5b41171869b1906a3923a7823d5af63a2e688bd327415cb27c03

prefetch "firewall.dll" sha1:4a010c72c199c4deb302553332a23407b6f8cd49 size:151552 http://software.bigfix.com/download/LoggingService/lib/firewall.dll  sha256:5a760aef3d1239b993adb7b89bc4e09e70db3b0c7cb619c9dc466c0130e333b5

delete "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\compliance.dll"
delete "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\firewall.dll"

copy "__Download\compliance.dll" "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\compliance.dll"
copy "__Download\firewall.dll" "{pathname of parent folder of file of service "BESClientLoggingService"}\lib\firewall.dll"

regset "[HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\LoggingService\Plugins\Locations]" "CA_FW"="{escape of (pathname of parent folder of file of service "BESClientLoggingService" & "\lib\firewall.dll")}"
regset "[HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\LoggingService\Plugins\Locations]" "BF_CP"="{escape of (pathname of parent folder of file of service "BESClientLoggingService" & "\lib\compliance.dll")}"

endif


//*********************** RESTART LOGGING SERVICE *******************//

waithidden "{pathname of parent folder of regapp "besclient.exe" & "\BESClientLoggingService\BESClientLoggingService.exe"}" -s

action may require restart]]></ActionScript>
		</DefaultAction>
	</Fixlet>
</BES>
