<?xml version="1.0" encoding="UTF-8"?>
<BES xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="BES.xsd">
	<Task>
		<Title>Uninstall BES Relay</Title>
		<Description><![CDATA[
<TABLE><TBODY><TR><TD><FONT size=2>This task will uninstall the BES Relay on the selected computers.</FONT></LI></UL></TD></TR></TBODY></TABLE>
]]></Description>
		<Relevance>exists relay service OR exists main gather service</Relevance>
		<Relevance>not exists main gather service</Relevance>
		<Relevance><![CDATA[version of client >= "5.1"]]></Relevance>
		<Relevance>exists relay service</Relevance>
		<Relevance>if exists property "in agent context" then (in agent context) else true</Relevance>
		<Relevance>(name of operating system) as lowercase does not contain "tinycore"</Relevance>
		<Category>BES Server/Relay Setting</Category>
		<DownloadSize>0</DownloadSize>
		<Source>IBM</Source>
		<SourceID><![CDATA[<Unspecified>]]></SourceID>
		<SourceReleaseDate>2013-03-01</SourceReleaseDate>
		<SourceSeverity><![CDATA[<Unspecified>]]></SourceSeverity>
		<MIMEField>
			<Name>x-fixlet-first-propagation</Name>
			<Value>Fri, 26 Aug 2005 01:01:28 +0000</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-domain_attributes</Name>
			<Value>BES Component Relay</Value>
		</MIMEField>
		<MIMEField>
			<Name>x-fixlet-modification-time</Name>
			<Value>Tue, 28 Mar 2017 17:03:44 +0000</Value>
		</MIMEField>
		<Delay>PT1S</Delay>
		<Action ID="Action1">
			<Description>
				<PreLink>Click </PreLink>
				<Link>here</Link>
				<PostLink> to uninstall the BES Relay.</PostLink>
			</Description>
			<ActionScript MIMEType="application/x-Fixlet-Windows-Shell"><![CDATA[if {name of operating system starts with "Win"}
	if {exists relay service AND ((it < "8.0") of (if (version of client >= "8.0") then (version of relay service) else (version of regapp "besrelay.exe")))}
		delete __appendfile
		appendfile [InstallShield Silent]
		appendfile Version=v7.00
		appendfile File=Response File
		appendfile [File Transfer]
		appendfile OverwrittenReadOnly=NoToAll
		move __appendfile "{pathname of folder "temp" of windows folder as string & "\uninst.iss"}"
		if {exists key "HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\Brand" whose (exists value "value" whose (it as string = "Trend") of it) of registry}
			wait {value "UninstallString" of key whose (value "DisplayName" of it = "ESP Relay" and name of it starts with "{") of key "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall" of registry} /s --uninstallsilent -f1"{pathname of folder "temp" of windows folder as string & "\uninst.iss"}"
		else
			wait {value "UninstallString" of key whose (value "DisplayName" of it = "BigFix Enterprise Relay" and name of it starts with "{") of key "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall" of registry} /s --uninstallsilent -f1"{pathname of folder "temp" of windows folder as string & "\uninst.iss"}"
		endif
		delete "{pathname of folder "temp" of windows folder as string & "\uninst.iss"}"
	else
		if {exists key "HKEY_LOCAL_MACHINE\SOFTWARE\BigFix\Brand" whose (exists value "value" whose (it as string = "Trend") of it) of registry}
			wait {preceding text of last "%00" of ((value "UninstallString" of key whose (value "DisplayName" of it = "Trend Micro Endpoint Security Platform Relay" and name of it starts with "{") of key "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall" of registry) as string) | ((value "UninstallString" of key whose (value "DisplayName" of it = "Trend Micro Endpoint Security Platform Relay" and name of it starts with "{") of key "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall" of registry) as string)} /qn
		else
			wait {preceding text of last "%00" of ((value "UninstallString" of key whose ((value "DisplayName" of it = "IBM Endpoint Manager Relay" OR value "DisplayName" of it = "BigFix Enterprise Relay" OR value "DisplayName" of it = "Tivoli Endpoint Manager Relay" OR value "DisplayName" of it = "IBM BigFix Relay") and name of it starts with "{") of key "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall" of registry) as string) | ((value "UninstallString" of key whose ((value "DisplayName" of it = "IBM Endpoint Manager Relay" OR value "DisplayName" of it = "BigFix Enterprise Relay" OR value "DisplayName" of it = "Tivoli Endpoint Manager Relay" OR value "DisplayName" of it = "IBM BigFix Relay") and name of it starts with "{") of key "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall" of registry) as string)} RESTARTBESCLIENT=0 /qn
		endif
	endif

elseif {name of operating system starts with "SunOS"}
	delete "{(client folder of current site as string) & "/__appendfile"}"
	appendfile mail=
	appendfile instance=overwrite
	appendfile partial=nocheck
	appendfile runlevel=nocheck
	appendfile idepend=nocheck
	appendfile space=ask
	appendfile setuid=nocheck
	appendfile conflict=nocheck
	appendfile action=nocheck
	appendfile basedir=default
	delete "{(client folder of current site as string) & "/solaris_admin_file"}"
	move "{(client folder of current site as string) & "/__appendfile"}" "{(client folder of current site as string) & "/solaris_admin_file"}"
	wait /usr/sbin/pkgrm -a "{(client folder of current site as string) & "/solaris_admin_file"}" -n BESRelay

elseif {exists match( regex "Ubuntu( Server|)( LTS|)" ) of name of operating system}
	wait /usr/bin/dpkg --purge BESRelay

elseif {name of operating system starts with "Linux"}
	wait /bin/rpm -e BESRelay

elseif {name of operating system starts with "AIX"}
	wait /usr/sbin/installp -u BESRelay
endif

// Handles the situations where the relay is installed, upgraded, or uninstalled.  Causes the local agent to re-select its relay.
// Some relay uninstalls may restart the agent
relay select]]></ActionScript>
			<SuccessCriteria Option="CustomRelevance">exists relay service</SuccessCriteria>
		</Action>
	</Task>
</BES>
